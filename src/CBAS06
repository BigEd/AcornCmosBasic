
;                                   FILE 7 prn and expression analyser > CBAS06

FCOMPS PLA
 STA IACCL
 PLA
 STA IACCM
 PLA
 STA IACCN
 PLA
 STA IACCH
 JSR PHFACC
 JSR IFLT
 JSR FTOW
 JSR POPSET
 JSR FLDA
 BRA FCMPA ;X preserved from ADDER to here
FCOMPR JSR PHFACC
 JSR ADDER
 TAY
 JSR FLOATI
 JSR POPSET
FCMP JSR FLDW
FCMPA LDYIM 0 ;X preserved from ADDER to here
 LDAIM &7F
 TRB FWRKS
 LDA FACCS
 ANDIM &80
 CMP FWRKS
 BNE FCMPZ
 LDA FWRKX
 CMP FACCX
 BNE FCMPZZ
 LDA FWRKMA
 CMP FACCMA
 BNE FCMPZZ
 LDA FWRKMB
 CMP FACCMB
 BNE FCMPZZ
 LDA FWRKMC
 CMP FACCMC
 BNE FCMPZZ
 LDA FWRKMD
 CMP FACCMD
 BNE FCMPZZ
FCMPZ RTS
FCMPZZ RORA
 EOR FWRKS
 ROLA
 LDAIM 1
 RTS
COMPRE JMP LETM
COMPR TXA  ;string or numeric compare
 BEQ STNCMP
 BMI FCOMPR
 LDA IACCH
 PHA
 LDA IACCN
 PHA
 LDA IACCM
 PHA
 LDA IACCL
 PHA
 JSR ADDER
 TAY
 BEQ COMPRE
 BMI FCOMPS
 LDA IACCH
 EORIM &80
 STA IACCH
 SEC
 PLA
 SBC IACCL
 STA IACCL
 PLA
 SBC IACCM
 TSB IACCL;iaccl=iaccl OR a
 PLA
 SBC IACCN
 TSB IACCL;iaccl=iaccl OR a
 PLA
 LDYIM 0
 EORIM &80
 SBC IACCH
 ORA IACCL
 RTS
STNCMP JSR PHSTR
 JSR ADDER
 TAY
 BNE COMPRE
 LDAI AESTKP ;other clen
 CMP CLEN
 BCC COMPRF
 LDA CLEN
COMPRF STA WORK
COMPRG CPY WORK ;Y was zero
 BEQ COMPRH
 INY
 LDAIY AESTKP
 CMPAY STRACC-1
 BEQ COMPRG
 BRA COMPRI
COMPRH LDAI AESTKP
 CMP CLEN
COMPRI PHP
 JSR POPSTX ;doesn't use X
 LDYIM 0
 PLP
 RTS ;has preserved X throughout
AEEXPR LDA LINE
 STA AELINE
 LDA LINE+1
 STA AELINE+1
 LDA CURSOR
 STA AECUR
;  EXPR reads a rhs. If status is eq then it returned a string.
;  If status is neq and plus then it returned a word.
;  If status is neq and minus then it returned an fp.
EXPR JSR ANDER
EXPRQ CPXIM TOR
 BEQ OR
 CPXIM TEOR
 BEQ EOR
 DEC AECUR
 TAY
 STA TYPE
 RTS
OR JSR PHANDR
 JSR INTEGY
 LDYIM 3
ORLP LDAIY AESTKP
 ORAAY IACCL
 STAAY IACCL
 DEY
 BPL ORLP
EXPRP JSR POPINC
 LDAIM &40
 BRA EXPRQ
EOR JSR PHANDR
 JSR INTEGY
 LDYIM 3
EORLP LDAIY AESTKP
 EORAY IACCL
 STAAY IACCL
 DEY
 BPL EORLP
 BRA EXPRP
PHANDR JSR INTEGY
 JSR PHACC
ANDER JSR RELATE
ANDERQ CPXIM TAND
 BEQ AND
 RTS
AND JSR INTEGY
 JSR PHACC
 JSR RELATE
 JSR INTEGY
 LDYIM 3
ANDLP LDAIY AESTKP
 ANDAY IACCL
 STAAY IACCL
 DEY
 BPL ANDLP
 JSR POPINC
 LDAIM &40
 BRA ANDERQ
RELATE JSR ADDER
 CPXIM "?"
 BCS RELATX
 CPXIM "<"
 BCS RELTS
RELATX RTS
RELTS BEQ RELTLT
 CPXIM ">"
 BEQ RELTGT
 TAX ;test equal
 JSR COMPR+1
 BNE FAIL
PASS DEY
FAIL STY IACCL
 STY IACCM
 STY IACCN
 STY IACCH
 LDAIM &40 ;type integer
 RTS
RELTLT TAX
 LDY AECUR
 LDAIY AELINE
 CMPIM "="
 BEQ LTOREQ
 CMPIM ">"
 BEQ NEQUAL
 JSR COMPR ;test less than
 BCC PASS
 BRA FAIL
LTOREQ INC AECUR
 JSR COMPR
 BEQ PASS
 BCC PASS
 BRA FAIL
NEQUAL INC AECUR
 JSR COMPR
 BNE PASS
 BRA FAIL
RELTGT TAX
 LDY AECUR
 LDAIY AELINE
 CMPIM "="
 BEQ GTOREQ
 JSR COMPR ;test greater than
 BEQ FAIL
 BCS PASS
 BRA FAIL
GTOREQ INC AECUR
 JSR COMPR
 BCS PASS
 BRA FAIL
STROVR BRK
 = &13
 = "String too long"
 BRK
STNCON JSR PHSTR
 JSR POWER
 TAY
 BNE ADDERE
 CLC
 PHX
 LDAI AESTKP
 ADC CLEN
 BCS STROVR
 TAX
 PHA
 LDY CLEN
CONLOP LDAAY STRACC-1
 STAAX STRACC-1
 DEX
 DEY
 BNE CONLOP
 JSR POPSTR
 PLA
 STA CLEN
 PLX
 LDAIM 0
 BRA ADDERQ
ADDER JSR TERM
ADDERQ CPXIM "+"
 BEQ PLUS
 CPXIM "-"
 BEQ MINUS
 RTS
PLUS TAY
 BEQ STNCON
 BMI FPLUS
 JSR PHTERM
 TAY
 BEQ ADDERE
 BMI FPLUST
 CLC
 LDAI AESTKP
 ADC IACCL
 STA IACCL
 LDYIM 1
 LDAIY AESTKP
 ADC IACCM
 STA IACCM
 INY
 LDAIY AESTKP
 ADC IACCN
 STA IACCN
 INY
 LDAIY AESTKP
 ADC IACCH
ADDERP STA IACCH
 CLC
 LDA AESTKP
 ADCIM 4
 STA AESTKP
 LDAIM &40 ;reset type
 BCC ADDERQ
 INC AESTKP+1
 BRA ADDERQ
ADDERE JMP LETM
FPLUS JSR PHFACC
 JSR TERM
 TAY
 BEQ ADDERE
 STX TYPE
 BMI FPLUSS
 JSR IFLT
FPLUSS JSR POPSET
 JSR FADD
FFFFA LDX TYPE
 LDAIM &FF
 BRA ADDERQ
FPLUST STX TYPE
 JSR POPACC
 JSR PHFACC
 JSR IFLT
 BRA FPLUSS
MINUS TAY
 BEQ ADDERE
 BMI FMINUS
 JSR PHTERM
 TAY
 BEQ ADDERE
 BMI FMINUT
 SEC
 LDAI AESTKP
 SBC IACCL
 STA IACCL
 LDYIM 1
 LDAIY AESTKP
 SBC IACCM
 STA IACCM
 INY
 LDAIY AESTKP
 SBC IACCN
 STA IACCN
 INY
 LDAIY AESTKP
 SBC IACCH
 BRA ADDERP
FMINUS JSR PHFACC
 JSR TERM
 TAY
 BEQ ADDERE
 STX TYPE
 BMI FMINUR
 JSR IFLT
FMINUR JSR POPSET
 JSR FXSUB
 BRA FFFFA
FMINUT STX TYPE
 JSR POPACC
 JSR PHFACC
 JSR IFLT
 JSR POPSET
 JSR FSUB
 BRA FFFFA
FTIMLF JSR IFLT
FTIML JSR POPACC
 JSR PHFACC
 JSR IFLT
 BRA FTIMR
FTIMFL JSR IFLT
FTIM JSR PHFACC
 JSR POWER
 TAY
 JSR FLOATI
FTIMR JSR POPSET
 JSR FMUL
 LDAIM &FF
 JMP TERMQ ;X has been preserved to here
FTIME JMP LETM
TIMES TAY
 BEQ FTIME
 BMI FTIM
 LDY IACCH
 CPY IACCN
 BNE FTIMFL
 LDA IACCM
 ASLA
 TYA
 ADCIM 0
 BNE FTIMFL
 JSR PHPOW
 TAY
 BEQ FTIME
 BMI FTIML
 LDY IACCH
 CPY IACCN
 BNE FTIMLF
 LDA IACCM
 ASLA
 TYA
 ADCIM 0
 BNE FTIMLF
 PHY ;Push IACCH
 JSR ABSCOM
 STX TYPE
 LDXIM WORK+2
 JSR ACCTOM
 JSR POPACC
 PLA
 EOR IACCH
 STA WORK
 JSR ABSCOM
 LDYIM 0
 LDXIM 0
 CLR WORK+8
 CLR WORK+9
NUL LSR WORK+3
 ROR WORK+2
 BCC NAD
 CLC
 TYA
 ADC IACCL
 TAY
 TXA
 ADC IACCM
 TAX
 LDA WORK+8
 ADC IACCN
 STA WORK+8
 LDA WORK+9
 ADC IACCH
 STA WORK+9
NAD ASL IACCL
 ROL IACCM
 ROL IACCN
 ROL IACCH
 LDA WORK+2
 ORA WORK+3
 BNE NUL
 ASSERT :MSB: NUL = :MSB: .
 STY WORK+6
 STX WORK+7
 LDA WORK
 PHP
REMIN LDXIM WORK+6
DIVIN JSR MTOACC
 PLP
 BPL TERMA
 JSR COMPNO
TERMA LDX TYPE
 BRA TERMQ
TIMESJ JMP TIMES
PHTERM JSR PHACC
TERM JSR POWER
TERMQ CPXIM "*"
 BEQ TIMESJ
 CPXIM "/"
 BEQ DIVIDE
 CPXIM TMOD
 BEQ REMAIN
 CPXIM TDIV
 BEQ INTDIV
 RTS
DIVIDE TAY
 JSR FLOATI
 JSR PHFACC
 JSR POWER
 STX TYPE
 TAY
 JSR FLOATI
 JSR POPSET
 JSR FXDIV
 LDAIM &FF
 BRA TERMA
REMAIN JSR DIVOP
 LDA WORK+1
 PHP
 BRA REMIN
INTDIV JSR DIVOP
 ROL WORK+2
 ROL WORK+3
 ROL WORK+4
 ROL WORK+5
 BIT WORK
 PHP
 LDXIM WORK+2
 BRA DIVIN
PHPOW JSR PHACC
POWER JSR FACTOR
POWERB PHA
POWERA LDY AECUR
 INC AECUR
 LDAIY AELINE
 CMPIM " "
 BEQ POWERA
 TAX
 PLA
 CPXIM "^"
 BEQ POW
 RTS
POW TAY
 JSR FLOATI
 JSR PHFACC
 JSR FLTFAC
 LDA FACCX
 CMPIM &87
 BCS FPOWA ;abs(n)>=64
 JSR FFRAC
 BNE FPOWE
 JSR POPSET
 JSR FLDA
 LDA FQUAD
 JSR FIPOW
 BRA FPOWF
FPOWE JSR STARGC
 LDA AESTKP
 STA ARGP
 LDA AESTKP+1
 STA ARGP+1
 JSR FLDA
 LDA FQUAD
 JSR FIPOW
FPOWC LDAIM FWSB
 JSR FSTAP
 JSR POPSET
 JSR FLDA
 JSR FLOG
 JSR ACMUL
 JSR FEXP
 LDAIM FWSB
 JSR ACMUL+2;*ARGB
FPOWF LDAIM &FF
 BRA POWERB
FPOWA JSR STARGC
 JSR FONE
 BRA FPOWC
POSITE LDAIM 0
 BRA NPRN+2
NPRN LDAIM 5 ;print number to chout 5 size in decimal
 STA PRINTS
 LDXIM 4
NUMLOP CLRAX WORK+8
 SEC
NUMLP LDA IACCL
 SBCAX VALL
 TAY
 LDA IACCM
 SBCAX VALM
 BCC OUTNUM
 STA IACCM
 STY IACCL
 INCAX WORK+8
 BRA NUMLP
OUTNUM DEX
 BPL NUMLOP
 LDXIM 5
LZB DEX
 BEQ LASTZ
 LDAAX WORK+8
 BEQ LZB
LASTZ STX WORK
 LDA PRINTS
 BEQ PLUME
 SBC WORK ;carry clear
 BEQ PLUME
 TAX
 JSR LISTPL
 LDX WORK
PLUME LDAAX WORK+8
 ORAIM &30
 JSR CHOUT
 DEX
 BPL PLUME
 RTS
 LNK CBAS07
